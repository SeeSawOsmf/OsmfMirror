<?xml version="1.0" encoding="utf-8"?>
<!--
/*****************************************************
*  
*  Copyright 2009 Akamai Technologies, Inc.  All Rights Reserved.
*  
*****************************************************
*  The contents of this file are subject to the Mozilla Public License
*  Version 1.1 (the "License"); you may not use this file except in
*  compliance with the License. You may obtain a copy of the License at
*  http://www.mozilla.org/MPL/
*   
*  Software distributed under the License is distributed on an "AS IS"
*  basis, WITHOUT WARRANTY OF ANY KIND, either express or implied. See the
*  License for the specific language governing rights and limitations
*  under the License.
*   
*  
*  The Initial Developer of the Original Code is Akamai Technologies, Inc.
*  Portions created by Akamai Technologies, Inc. are Copyright (C) 2009 Akamai 
*  Technologies, Inc. All Rights Reserved. 
*  
*****************************************************/
-->
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" layout="absolute" backgroundColor="#000000" 
	xmlns:samples="org.osmf.samples.*" applicationComplete="init()">
	<mx:Style source="CuePointSample.css" />
	<mx:Script>
		<![CDATA[
			import mx.collections.ArrayCollection;
			import org.osmf.elements.CuePoint;
			import org.osmf.elements.CuePointType;
			import org.osmf.events.TemporalFacetEvent;
			import org.osmf.metadata.Metadata;
			import org.osmf.metadata.TemporalFacet;
			import org.osmf.metadata.TemporalFacetKey;
			import org.osmf.metadata.MetadataNamespaces;
			import org.osmf.traits.TimeTrait;
			import org.osmf.traits.LoadTrait;
			import org.osmf.traits.MediaTraitType;
			import org.osmf.traits.LoadState;
			import org.osmf.elements.VideoElement;
			import org.osmf.net.NetLoader;
			import org.osmf.net.NetStreamCodes;
			import org.osmf.media.URLResource;
			import org.osmf.events.*;
						
			private static const REMOTE_PDL:String = "http://mediapm.edgesuite.net/osmf/content/test/cuepoints/spacealonehd_sounas_640_400.flv";
			private static const REMOTE_STREAM:String = "rtmp://cp67126.edgefcs.net/ondemand/mp4:mediapm/osmf/content/test/cuepoints/spacealonehd_sounas_640.f4v";
			private static const REMOTE_STREAM_WITH_NAV:String = "rtmp://cp67126.edgefcs.net/ondemand/mp4:mediapm/osmf/content/test/cuepoints/spacealonehd_sounas_640_with_nav.f4v";
						
			private static const DEFAULT_PROGRESS_DELAY:uint = 100;
			private static const MAX_VIDEO_WIDTH:int = 480;
			private static const MAX_VIDEO_HEIGHT:int = 270;
			
			private static const CUSTOM_NAMESPACE:String = "org.osmf.cuePointSample.ascuepoint";	
						
			private var sliderDragging:Boolean;
			private var waitForSeek:Boolean;
			private var ignoreDynamicCuePoints:Boolean;
			private var _temporalFacet:TemporalFacet;
			
			[Bindable]
			private var _cuePointsCollection:ArrayCollection;
			
			private var videoElement:VideoElement;
			
			public static function timeCode(sec:Number):String 
			{
				var h:Number = Math.floor(sec/3600);
				h = isNaN(h) ? 0 : h;
				
				var m:Number = Math.floor((sec%3600)/60);
				m = isNaN(m) ? 0 : m;
				
				var s:Number = Math.floor((sec%3600)%60);
				s = isNaN(s) ? 0 : s;
				
				return (h == 0 ? "":(h<10 ? "0"+h.toString()+":" : h.toString()+":"))+(m<10 ? "0"+m.toString() : m.toString())+":"+(s<10 ? "0"+s.toString() : s.toString());
			}
				
			private function init():void
			{
				mediaPlayerWrapper.mediaPlayer.addEventListener(DisplayObjectEvent.MEDIA_SIZE_CHANGE, onMediaSizeChange);		
				mediaPlayerWrapper.mediaPlayer.addEventListener(TimeEvent.DURATION_CHANGE, onDurationChange);	
				mediaPlayerWrapper.mediaPlayer.addEventListener(TimeEvent.CURRENT_TIME_CHANGE, onCurrentTimeChange);
				mediaPlayerWrapper.mediaPlayer.addEventListener(SeekEvent.SEEKING_CHANGE, onSeekingChange);
				
				mediaPlayerWrapper.mediaPlayer.currentTimeUpdateInterval = DEFAULT_PROGRESS_DELAY;
								
				sliderDragging = false;
				waitForSeek = false;
				ignoreDynamicCuePoints = false;

				videoElement = new VideoElement(new URLResource(REMOTE_STREAM_WITH_NAV));
				
				// Listen for traits to be added, so we can adjust the UI. For example, enable the seek bar
				// when ISeekable is added
				videoElement.addEventListener(MediaElementEvent.TRAIT_ADD, onTraitAdd);
				
				// Listen for metadata to be added so we can add any desired event listeners on any
				// metadata facets we care about. For example, the temporalFacet.
				videoElement.metadata.addEventListener(MetadataEvent.FACET_ADD, onFacetAdd);
				
				// Listen for metadata to be removed so we remove an event listeners
				videoElement.metadata.addEventListener(MetadataEvent.FACET_REMOVE, onFacetRemove);
																
				mediaPlayerWrapper.element = videoElement;
				enablePlayerControls(true);
				
			}
			
			private function onCuePoint(event:TemporalFacetEvent):void
			{
				var cuePoint:CuePoint = event.key as CuePoint;
				var ns:String = (event.currentTarget as TemporalFacet).namespaceURL;
				
				var diff:Number = cuePoint.time - mediaPlayerWrapper.mediaPlayer.currentTime;
				trace("cuePoint.time="+cuePoint.time+", currentTime time="+this.mediaPlayerWrapper.mediaPlayer.currentTime+", diff="+diff);
								
				// If we are getting embedded cue points, let's ignore the dynamic cue points coming from
				// the framework, these are being dispatched from the list of cue points found in the 
				// metadata for the file.
				if (ns == MetadataNamespaces.TEMPORAL_EMBEDDED_METADATA)
				{
					ignoreDynamicCuePoints = true;
				}
				else if (ignoreDynamicCuePoints && ns == MetadataNamespaces.TEMPORAL_DYNAMIC_METADATA)
				{
					return;
				}				

				showCuePointEvent(cuePoint, ns);
			}
			
			private function showCuePointEvent(cuePoint:CuePoint, ns:String):void
			{
				// Don't show navigation cue points in the cue point event display
				if (cuePoint.type == CuePointType.NAVIGATION)
				{
					return;
				}
				
				var msg:String = "time: "+timeCode(cuePoint.time)+", name: \""+cuePoint.name+"\", type: \""+cuePoint.type+"\", namespace: \""+ns+"\"";
				taCuePointEvents.text += msg + "\n";
				
				callLater(autoScroll);
			}
			
			private function onClickClearEvents(event:Event):void
			{
				taCuePointEvents.text = "";
			}
			
			private function onClickInsertMarker(event:Event):void
			{
				taCuePointEvents.text += "--------------------------------------------------------------------------\n";	
			}	
					
			private function autoScroll():void 
			{
				taCuePointEvents.verticalScrollPosition = taCuePointEvents.maxVerticalScrollPosition;
			}
											
			private function onMediaSizeChange(event:DisplayObjectEvent):void 
			{
				var width:int = event.newWidth;
				var height:int = event.newHeight;
				
				// Scale to native or smaller
				if (width > MAX_VIDEO_WIDTH || height > MAX_VIDEO_HEIGHT)
				{
					if ((width/height) >= (MAX_VIDEO_WIDTH/MAX_VIDEO_HEIGHT))
					{
						mediaPlayerWrapper.width = MAX_VIDEO_WIDTH;
						mediaPlayerWrapper.height = MAX_VIDEO_WIDTH * (height/width);
					}
					else
					{
						mediaPlayerWrapper.width = MAX_VIDEO_HEIGHT * (width/height);
						mediaPlayerWrapper.height = MAX_VIDEO_HEIGHT;
					}
				}
				else if (width > 0 && height > 0)
				{
					mediaPlayerWrapper.width = event.newWidth;
					mediaPlayerWrapper.height = event.newHeight;			
				}
			}
			
			private function onDurationChange(event:TimeEvent):void
			{
				seekBar.maximum = event.time;
				lblDuration.text = timeCode(event.time);
			}
						
			private function onCurrentTimeChange(event:TimeEvent):void
			{
				if (mediaPlayerWrapper.mediaPlayer.temporal && !sliderDragging && !waitForSeek)
				{
					seekBar.value = event.time;
					lblPlayhead.text = timeCode(event.time);
				}
			}
			
			private function onSeekingChange(event:SeekEvent):void
			{
				if (event.seeking == false)
				{
					waitForSeek = false;
				}
			}

   			private function toggleDragging(state:Boolean):void
   			{
   				sliderDragging = state;
   				if (!state)
   				{
   					waitForSeek = true;
   					if (mediaPlayerWrapper.mediaPlayer.canSeek)
   					{
   						mediaPlayerWrapper.mediaPlayer.seek(seekBar.value); 
   					}
   				}
   			}
   			
   			private function onTraitAdd(event:MediaElementEvent):void
   			{
   				switch (event.traitType)
   				{
   					case MediaTraitType.SEEK:
   						seekBar.enabled = seekBar.visible = true;
   						break;
   				}	
   			}
   			
   			private function onFacetAdd(event:MetadataEvent):void
   			{
				var temporalFacet:TemporalFacet = event.facet as TemporalFacet;
				
				if (temporalFacet)
				{
					trace(">>> Temporal facet added, namespace="+temporalFacet.namespaceURL);
					temporalFacet.addEventListener(TemporalFacetEvent.TIME_REACHED, onCuePoint);
					
					// Show all the cue points found in the media (via an unsupported API).
					if (_cuePointsCollection == null && temporalFacet.namespaceURL == MetadataNamespaces.TEMPORAL_DYNAMIC_METADATA)
					{
						this._cuePointsCollection = new ArrayCollection();
						for (var i:int = 0; i < temporalFacet.numValues; i++)
						{
							this._cuePointsCollection.addItem(temporalFacet.getValueAt(i));
						}
					}
				}
   			}
   			
   			private function onFacetRemove(event:MetadataEvent):void
   			{
   				var temporalFacet:TemporalFacet = event.facet as TemporalFacet;
   				
   				if (temporalFacet)
   				{
   					trace(">>> Temporal facet removed, namespace="+temporalFacet.namespaceURL);
   					temporalFacet.removeEventListener(TemporalFacetEvent.TIME_REACHED, onCuePoint);
   				}
   			}
   									
			private function onClickPlayBtn():void
			{
				if (mediaPlayerWrapper.mediaPlayer.playing && mediaPlayerWrapper.mediaPlayer.canPause)
				{
					playBtn.label = "Play";
					mediaPlayerWrapper.mediaPlayer.pause();
				}
				else if (mediaPlayerWrapper.mediaPlayer.paused && mediaPlayerWrapper.mediaPlayer.canPlay)
				{
					playBtn.label = "Pause";
					mediaPlayerWrapper.mediaPlayer.play();
				}
			}
			
			private function onClickAddCuePoint(event:Event):void
			{
				if (_temporalFacet == null)
				{
					_temporalFacet = new TemporalFacet(CUSTOM_NAMESPACE, videoElement);
					videoElement.metadata.addFacet(_temporalFacet);				
				}						
			
				var cuePoint:CuePoint = new CuePoint(CuePointType.ACTIONSCRIPT, Number(tiCuePointTime.text), tiCuePointName.text, null);
				_temporalFacet.addValue(cuePoint, cuePoint);
				
				updateInternalCollection(cuePoint);
				
				tiCuePointTime.text = tiCuePointName.text = "";
			}
			
			private function updateInternalCollection(newCuePoint:CuePoint):void
			{
				// See if there is an existing value, if so replace it
				for (var i:int = 0; i < _cuePointsCollection.length; i++)
				{
					if ((_cuePointsCollection[i] as TemporalFacetKey).time == newCuePoint.time)
					{
						_cuePointsCollection.removeItemAt(i);
						break;
					}
				}
				
				_cuePointsCollection.addItem(newCuePoint);
			}

			private function onClickCuePoint(event:Event):void
			{
				var cuePoint:CuePoint = this.gridCuePoints.selectedItem as CuePoint;
				
				if (cuePoint.type == CuePointType.NAVIGATION && this.mediaPlayerWrapper.mediaPlayer.canSeek)
				{
					this.mediaPlayerWrapper.mediaPlayer.seek(cuePoint.time);
				}
			}
			
			private function enablePlayerControls(enable:Boolean=true):void
			{
				playBtn.enabled = seekBar.enabled = enable;
			}
						
		]]>
	</mx:Script>
	
	<mx:VBox id="mainContainer" paddingLeft="20" paddingTop="10">
		<mx:HBox width="100%">
			<mx:Label styleName="title" text="OSMF Cue Point Sample" />
		</mx:HBox>
		<mx:Spacer height="5" />
		<mx:HBox>
			<mx:VBox>
			    <samples:MediaPlayerWrapper id="mediaPlayerWrapper" width="480" height="270"  />
	    		<mx:HSlider id="seekBar" width="480" thumbPress="toggleDragging(true)" thumbRelease="toggleDragging(false)" enabled="false" />
	    		<mx:HBox horizontalAlign="right" width="100%">
	    			<mx:Label text="Position: " />
					<mx:Label id="lblPlayhead" width="100" styleName="timeCode" />
					<mx:Label text="Duration: " />
	    			<mx:Label id="lblDuration" width="100" styleName="timeCode" />			
					<mx:Button id="playBtn" label="Pause" click="onClickPlayBtn()" enabled="false" />
	    		</mx:HBox>			
			</mx:VBox>
			<mx:Spacer width="10"/>
			<mx:VBox backgroundColor="#333333" verticalGap="0">
				<mx:Label text="Cue Points in the Media:" />
				<mx:DataGrid id="gridCuePoints" dataProvider="{_cuePointsCollection}" height="265" width="250" itemClick="onClickCuePoint(event)">
					<mx:columns>
						<mx:DataGridColumn headerText="Time" dataField="time" textAlign="right" width="50" sortable="true" >
							<mx:itemRenderer>
								<mx:Component>
									<mx:Label text="{CuePointSample.timeCode(data.time)}" color="#000000"/>
								</mx:Component>
							</mx:itemRenderer>
						</mx:DataGridColumn>
						<mx:DataGridColumn headerText="Name" dataField="name" sortable="false" />
						<mx:DataGridColumn headerText="Type" dataField="type" sortable="false">
							<mx:itemRenderer>
								<mx:Component>
									<mx:Label text="{data.type}" color="#000000"/>
								</mx:Component>
							</mx:itemRenderer>
						</mx:DataGridColumn>
					</mx:columns>
				</mx:DataGrid>
			</mx:VBox>			
		</mx:HBox>
		<mx:Spacer height="10"/>
		<mx:HBox>
			<mx:VBox>
				<mx:Label text="Cue Point Events:" />
				<mx:TextArea id="taCuePointEvents" wordWrap="false" editable="false" width="480" height="124"/>
				<mx:HBox>
					<mx:Button label="Clear" click="onClickClearEvents(event)"/>
					<mx:Button label="Insert Marker" click="onClickInsertMarker(event)"/>
				</mx:HBox>
			</mx:VBox>
			<mx:Spacer width="10" />
			<mx:VBox>
				<mx:Label text="Add a cue point:" />
				<mx:VBox borderStyle="solid" paddingLeft="2" paddingTop="2" height="124" width="250">
			        <mx:Form width="100%" height="100%">
			            <mx:FormItem label="Time:">
			            	<mx:HBox>
							<mx:TextInput id="tiCuePointTime" width="40" />
							<mx:Label text="(seconds)"/>		            		
			            	</mx:HBox>
			            </mx:FormItem>
			            <mx:FormItem label="Name:">
							<mx:TextInput id="tiCuePointName" width="150" />
			            </mx:FormItem>
						<mx:Button label="Add" click="onClickAddCuePoint(event)"/>		            
			        </mx:Form>
				</mx:VBox>		
			</mx:VBox>
		</mx:HBox>
	</mx:VBox>
</mx:Application>
